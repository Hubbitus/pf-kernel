pkgname=('kernel37-pf' 'kernel37-pf-headers')
pkgver=3.7.1
pkgrel=pf
pkgdesc="pf-kernel with modules"
arch=('i686' 'x86_64')
makedepends=('xz' 'rsync')
options=('!strip')
license=('GPL')
url="http://pf.natalenko.name"

build() {
  # Go to kernel's tree root
  cd $startdir

  # Remove depmod from kernel script, steal this trick from Arch
  sed -i '2iexit 0' scripts/depmod.sh

  # Detect CPUs count automatically
  CPUS_COUNT=`cat /proc/cpuinfo | grep processor | wc -l`
  echo "Compiling using $CPUS_COUNT thread(s)"
  make -j$CPUS_COUNT bzImage modules || return 1
}

package_kernel37-pf() {
  depends=('coreutils' 'linux-firmware' 'kmod' 'mkinitcpio')
  provides=('kernel37')
  install='kernel37.install'

  cd $startdir

  # Note that modules are in /usr/lib/modules now
  mkdir -p $pkgdir/{usr/lib/modules,boot}
  make INSTALL_MOD_PATH=$pkgdir/usr modules_install || return 1

  # Running depmod for installed modules
  depmod -b "$pkgdir/usr" -F System.map "$pkgver-$pkgrel"

  # There's no separation of firmware depending on kernel version -
  # comment this line if you intend on using the built kernel exclusively,
  # otherwise there'll be file conflicts with the existing kernel
  rm -rf $pkgdir/usr/lib/firmware

  rm -f $pkgdir/usr/lib/modules/$pkgver-$pkgrel/{source,build}

  install -Dm644 "System.map" "$pkgdir/boot/System.map37-$pkgrel"
  install -Dm644 "arch/x86/boot/bzImage" "$pkgdir/boot/vmlinuz37-$pkgrel"
  install -Dm644 "distro/archlinux/linux-pf37.preset" "$pkgdir/etc/mkinitcpio.d/linux-pf37.preset"

  # Change the version strings in kernel37.install
  sed -i \
	-e "s/KERNEL_VERSION=.*/KERNEL_VERSION=\"$pkgver\"/" \
	-e "s/LOCAL_VERSION=.*/LOCAL_VERSION=\"-$pkgrel\"/" \
	$startdir/kernel37.install
}

package_kernel37-pf-headers() {
  provides=('kernel37-headers')

  cd $startdir

  mkdir -p $pkgdir/usr/lib/modules/$pkgver-$pkgrel/
  cd $pkgdir/usr/lib/modules/$pkgver-$pkgrel/
  ln -s ../../../src/linux-$pkgver-$pkgrel build

  cd $startdir

  mkdir -p $pkgdir/usr/src/linux-$pkgver-$pkgrel
  make INSTALL_HDR_PATH=$pkgdir/usr/src/linux-$pkgver-$pkgrel headers_install
  install -Dm644 .config $pkgdir/usr/src/linux-$pkgver-$pkgrel
  install -Dm644 Module.symvers $pkgdir/usr/src/linux-$pkgver-$pkgrel
  rsync -a --include='*/' --include='Kbuild*' --include='Kconfig*' --include='Makefile' --exclude='*' --prune-empty-dirs . $pkgdir/usr/src/linux-$pkgver-$pkgrel
  rsync -a scripts $pkgdir/usr/src/linux-$pkgver-$pkgrel
}
